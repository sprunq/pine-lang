use base::located::Located;
use base::source_id::SourceId;
use crate::token::Token;
use crate::ast::stmt::*;
use crate::ast::expr::*;
use crate::ast::op::*;
use crate::ast::ty::*;
use crate::ast::*;
use messages::lexer::LexerError;

grammar<'input>(file_id : SourceId);

extern {
    type Location = usize;
    type Error = LexerError;

    enum Token {
        "(" => Token::LtParen,
        ")" => Token::RtParen,
        "{" => Token::LtBrace,
        "}" => Token::RtBrace,
        "," => Token::Comma,
        "." => Token::Dot,
        "-" => Token::Minus,
        "+" => Token::Plus,
        ";" => Token::Semicolon,
        "/" => Token::Slash,
        "%" => Token::Modulo,
        "*" => Token::Asterisk,
        ":" => Token::Colon,
        "->" => Token::ArrowRight,

        "!" => Token::Bang,
        "!=" => Token::BangEqual,
        "=" => Token::Equal,
        "==" => Token::EqualEqual,
        ">" => Token::Greater,
        ">=" => Token::GreaterEqual,
        "<" => Token::Less,
        "<=" => Token::LessEqual,

        identifier => Token::Identifier(<String>),
        string => Token::String(<String>),
        integer => Token::Integer(<i64>),
        float => Token::Float(<f64>),

        "and" => Token::And,
        "type" => Token::Type,
        "else" => Token::Else,
        "false" => Token::False,
        "fun" => Token::Fun,
        "if" => Token::If,
        "or" => Token::Or,
        "return" => Token::Return,
        "break" => Token::Break,
        "true" => Token::True,
        "let" => Token::Let,
        "loop" => Token::Loop,

        "bool" => Token::TyBool,
        "i8" => Token::TyI8,
        "i32" => Token::TyI32,
        "i64" => Token::TyI64,
        "u8" => Token::TyU8,
        "u32" => Token::TyU32,
        "u64" => Token::TyU64,
        "f32" => Token::TyF32,
        "f64" => Token::TyF64,
        "str" => Token::TyStr,
    }
}


// --------------------------------------------------------------------------------------------------
//                                          Helpers
// --------------------------------------------------------------------------------------------------

// Parses an object of type T, and returns a Located<T>
Located<T>: Located<T> = {
    <start: @L> <data: T> <end: @R> => Located::new(file_id, start..end, data),
};

// Parses a comma separated list of T, and returns a Vec<T>
Comma<T>: Vec<T> = { 
    <mut v:(<T> ",")*> <e:T?> => match e { 
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};


// --------------------------------------------------------------------------------------------------
//                                          Declarations
// --------------------------------------------------------------------------------------------------

pub Program: Program = <stmts:Located<Declaration>*> => Program { <> };

Declaration : Declaration = { 
    DeclTypeObject, 
    DeclFunction
}

DeclTypeObject: Declaration = <decl:DeclTypeObjectInner> => Declaration::TypeObject(<>);
DeclTypeObjectInner: TypeObject = "type" <name:Identifier> "=" "{" <members:Comma<TypedParam>> "}" => TypeObject { <> };

DeclFunction: Declaration = <decl:DeclFunctionInner> => Declaration::Fun(<>);
DeclFunctionInner: StmtFun = "fun" <name:Identifier> "(" <params:Comma<TypedParam>> ")" "->" <ret_ty:Located<Type>>  <body:StmtBlockInner> => StmtFun { <> };

// --------------------------------------------------------------------------------------------------
//                                          Statements
// --------------------------------------------------------------------------------------------------

Stmt: Stmt = {
    StmtVarDecl,
    StmtIf,
    StmtLoop,
    StmtBlock,
    StmtExpr,
    StmtReturn,
    StmtBreak,
    StmtEmpty,
}

StmtVarDecl: Stmt = "let" <var:Identifier> ":" <ty:Located<Type>> "=" <value:Located<Expr>> ";" => Stmt::VariableDeclaration(VariableDeclaration { <> });
StmtEmpty: Stmt = <l:@L> ";" <r:@R>  => Stmt::Empty(StmtEmpty{ span : Located::empty(file_id, l..r)});
StmtExpr: Stmt = <value:Located<Expr>> ";" => Stmt::Expr(StmtExpr { <> });
StmtReturn: Stmt = "return" <value:Located<Expr>?> ";" => Stmt::Return(StmtReturn { <> });
StmtBreak: Stmt =  <l:@L> "break" ";" <r:@R> => Stmt::Break(StmtBreak { span : Located::empty(file_id, l..r) });

StmtLoop: Stmt = StmtLoopInner => Stmt::Loop(Box::new(<>));
StmtLoopInner: StmtLoop = "loop" <body:Located<StmtBlock>> => StmtLoop { <> };

StmtBlock: Stmt = StmtBlockInner => Stmt::Block(<>);
StmtBlockInner: StmtBlock = "{" <stmts:Located<Stmt>*> "}" => StmtBlock { <> };

StmtIf: Stmt = {
    StmtIfInner => Stmt::If(Box::new(<>)),
}
StmtIfInner: StmtIf = "if" "(" <cond:Located<Expr>> ")" <then:Located<StmtBlock>> <optional_else : ("else" <Located<StmtBlock>>)?> => { 
    match optional_else {
        Some(els) => StmtIf { cond, then, else_: Some(els) },
        None => StmtIf { cond, then, else_: None },
    }
};
    


// --------------------------------------------------------------------------------------------------
//                                          Expressions
// --------------------------------------------------------------------------------------------------

Expr : Expr = {
    ExprAssign,
    ExprLogicOr,
}

ExprAssign : Expr = <var:Located<ExprCall>> "=" <value:Located<Expr>> => Expr::Assign(Box::new(ExprAssign { <> }));

ExprLogicOr = ExprInfix<ExprLogicOr, OpLogicOr, ExprLogicAnd>;
ExprLogicAnd = ExprInfix<ExprLogicAnd, OpLogicAnd, ExprEquality>;
ExprEquality = ExprInfix<ExprEquality, OpEquality, ExprComparison>;
ExprComparison = ExprInfix<ExprComparison, OpComparison, ExprTerm>;
ExprTerm = ExprInfix<ExprTerm, OpTerm, ExprFactor>;
ExprFactor = ExprInfix<ExprFactor, OpFactor, ExprPrefix>;

OpLogicOr: OpInfix = "or" => OpInfix::LogicOr;

OpLogicAnd: OpInfix = "and" => OpInfix::LogicAnd;

OpEquality: OpInfix = {
    "==" => OpInfix::Equal,
    "!=" => OpInfix::NotEqual,
}

OpComparison: OpInfix = {
    ">" => OpInfix::Greater,
    ">=" => OpInfix::GreaterEqual,
    "<" => OpInfix::Less,
    "<=" => OpInfix::LessEqual,
}

OpTerm: OpInfix = {
    "+" => OpInfix::Add,
    "-" => OpInfix::Subtract,
}

OpFactor: OpInfix = {
    "*" => OpInfix::Multiply,
    "/" => OpInfix::Divide,
    "%" => OpInfix::Modulo,
}

ExprInfix<Lt, Op, Rt>: Expr = {
    <lt:Located<Lt>> <op:Op> <rt:Located<Rt>> => Expr::Infix(Box::new(ExprInfix { <> })),
    Rt,
}

ExprPrefix: Expr = {
    <op:OpPrefix> <rt:Located<ExprPrefix>> => Expr::Prefix(Box::new(ExprPrefix { <> })),
    ExprCall,
}

OpPrefix: OpPrefix = {
    "-" => OpPrefix::Negate,
    "!" => OpPrefix::Not,
}

ExprCall: Expr = {
    <callee:Located<ExprCall>> "(" <args:Comma<Located<Expr>>> ")" => Expr::Call(Box::new(ExprCall { <> })),
    <object:Located<ExprCall>> "." <member_name:Identifier> => Expr::MemberAccess(Box::new(ExprMemberAccess { <> })),
    ExprPrimary,
}

ExprPrimary: Expr = {
    StructInit,
    LiteralExpr,
    ExprVariable,
    "(" <Expr> ")",
}

StructInit: Expr = <name:Identifier> "{" <members: Comma<StructMemberInitPair>> "}" => Expr::StructureInit(ExprStructureInit { <> });
StructMemberInitPair: (Identifier, Located<Expr>) = { <name:Identifier> ":" <value:Located<Expr>> => (<>) }
ExprVariable: Expr = <var:Identifier> => Expr::Var(ExprVar { <> });
Identifier : Identifier = <name:identifier> => Identifier { <> };

LiteralExpr : Expr = {
    "false" => Expr::Literal(ExprLiteral::Bool(false)),
    "true" => Expr::Literal(ExprLiteral::Bool(true)),
    string => Expr::Literal(ExprLiteral::String(<>)),
    integer => Expr::Literal(ExprLiteral::Integer(<>)),
    float => Expr::Literal(ExprLiteral::Float(<>)),
}


// --------------------------------------------------------------------------------------------------
//                                             Types
// --------------------------------------------------------------------------------------------------

TypedParam : TypedParam  = {
    <name:Located<Identifier>> ":" <ty:Located<Type>> => TypedParam { <> },
}

Type : Type = {
    "(" ")" => Type::Unit,
    "bool" => Type::Bool,
    "i8" => Type::I8,
    "i32" => Type::I32,
    "i64" => Type::I64,
    "u8" => Type::U8,
    "u32" => Type::U32,
    "u64" => Type::U64,
    "f32" => Type::F32,
    "f64" => Type::F64,
    "str" => Type::String,
    <identifier> => Type::Struct(<>),
}
